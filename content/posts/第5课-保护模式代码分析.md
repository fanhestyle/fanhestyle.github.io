+++
author = "FanHe"
title = "第5课 保护模式代码分析"
date = 2022-04-18T16:50:01+08:00
description = ""
categories = [
 "从零打造操作系统"
]
draft = false
+++

## 

## 1. 概述

本文对保护模式代码进行简要的分析，并给出当前内存中的布局，首先给出保护模式加载的代码

```asm6502
.code16

.include "boot.inc"

.section .text
LOADER_STACK_TOP = LOADER_BASE_ADDR

jmp loader_start


GDT_BASE:  
    .long 0x0
    .long 0x0

CODE_DESC:
    .long 0x0000FFFF
    .long DESC_CODE_HIGH4

DATA_STACK_DESC:
    .long 0x0000FFFF
    .long DESC_DATA_HIGH4

//显存段的段基址是0xb8000
//显存段的段界限是 0xbffff-0xb8000 = 0x7fff 粒度是4k
//所以计算出的低位数字是 0x7fff/4k = 7
VIDEO_DESC:
    .long 0x80000007
    .long DESC_VIDEO_HIGH4

GDT_SIZE = .-GDT_BASE
GDT_LIMIT = GDT_SIZE - 1

.fill 60, 8, 0

//定义3个选择子
//相当于(CODE_DESC - GDT_BASE)/8 + TI_GDT + RPL0
.equ SELECTOR_CODE, (0x0001 << 3) + TI_GDT + RPL0
.equ SELECTOR_DATA, (0x0002 << 3) + TI_GDT + RPL0
.equ SELECTOR_VIDEO, (0x0003 << 3) + TI_GDT + RPL0

gdt_ptr:
    .word GDT_LIMIT
    .long GDT_BASE

loadermsg:
    .ascii "2 loader in real."
.equ MSG_SIZE, .-loadermsg


loader_start:

//调用BIOS打印字符串例程 INT 0x10, 功能号 0x13

movw $LOADER_BASE_ADDR, %sp  
movw $loadermsg, %bp
movw $MSG_SIZE , %cx
movw $0x1301, %ax
movw $0x001f, %bx
movw $0x1800, %dx
int $0x10

//准备进入保护模式
//步骤：
//  1. 打开A20
//  2. 加载gdt
//  3. cr0寄存器第1位置为1


inb $0x92, %al
orb $0x2, %al
outb %al, $0x92

lgdt gdt_ptr

movl %cr0, %eax
or $0x1, %eax
movl %eax, %cr0

ljmp $SELECTOR_CODE, $p_mode_start

.code32

p_mode_start:
    movw $SELECTOR_DATA, %ax
    movw %ax, %ds
    movw %ax, %es
    movw %ax, %ss
    movl $LOADER_STACK_TOP, %esp
    movw $SELECTOR_VIDEO, %ax
    movw %ax, %gs

    movb $'P', %gs:160

    jmp .
```

## 2. 段描述符的构造

代码中一共构造了3个段描述符，分别是：

（1）代码段描述符

（2）数据段描述符

（3）显存数据段描述符

另外还有一项是0的描述符，在GDT中的第一项必须是空，这样设计的目的是：一旦我们没有初始化段描述符，会导致CPU访问第一个位置的段描述符，于是CPU可以给出异常中断，于是我们可以根据这个提示修改我们的代码，检查是否是哪个段描述符忘记初始化，避免之后导致严重的错误。

构造段描述符的方式在之前的课程中有介绍，分别是对8个字节的每一位进行设置（关于段描述符每一位的含义简单介绍一下）

```
段描述符
高32位

31--24          基址31-24位
23 G            G位(段界限按4k/1B计算)
22 D/B          操作数大小（IP和SP是16位取址0还是32位取址1）        
21 L            64位/32位处理器
20 AVL          Avaliable保留给应用程序
19--16             段界限19-16位
15  P               存在段，可以置换到硬盘
14--13 DPL          描述符特权级(0-4)
12 S                S位（标记是系统段0/非系统段1）
11--8 TYPE          段的属性描述
7--0                段基址23~16位

低32位
31---16             段基址 15~0位
15---0              段界限 15~0位
```

我们上面的代码构造出的段描述符的特征是：

（1）第0索引位置：0 段描述符（按照GDT的要求设置一个空的描述符）

（2）第1索引位置：代码段描述符（采用了平坦模型：也就是段的基址是0，段的偏移地址最大可以到4G）

（3）第2索引位置：数据/栈段描述符（也采用了平坦模型，段的基址是0，段的偏移地址最大扩展到4G）

（4）第3索引位置：显存文本显示描述符，它采用的不是平坦模型，它的基址的0xB8000，可以偏移的大小最大扩展到0x7fff（这就是为什么最低为是7，因为0x7fff/4k = 7)

我们还定义了3个选择子，分别是

- SELECTOR_CODE值是8

- SELECTOR_DATA值是16

- SELECTOR_VIDEO值是24

正好就是我们的三个描述符相对于GDTR的偏移位置（字节单位），于是我们可以直接使用这三个选择子加载到段寄存器中来操作对应的内存空间

## 3. 代码执行后的内存布局

在执行上述代码之后的内存布局如下图所示：

<img src="/img/osdev/protected-memory-layout.png" title="" alt="保护模式内存布局" data-align="center">

# 

# 
