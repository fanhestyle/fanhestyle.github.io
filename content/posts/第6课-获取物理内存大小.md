+++
author = "FanHe"
title = "第6课 获取物理内存大小"
date = 2022-04-19T08:38:01+08:00
description = ""
categories = [
 "从零打造操作系统"
]
draft = false
+++

## 

## 1. 概述

在我们继续迈进内核编程之前，首先需要能获取到当前硬件的一些信息，首要的信息是物理内存的大小，一般来说可以用BIOS中断0x15实现，分别是BIOS中断0x15的3个子功能，子功能号要存放到寄存器EAX或AX中，这3个子功能分别是：

1. EAX=0xE820：遍历主机上全部内存。

2. AX=0xE801：分别检测低15MB和16MB～4GB的内存，最大支持4GB。

3. AH=0x88：最多检测出64MB内存，实际内存超过此容量也按照64MB返回

从1到3的功能依次减弱，但是编码难度也是递减的，有时候为了简单获取内存大小，在满足需要的前提下可以使用2或者3完成，编写代码相对容易（主要是1需要额外的类似结构体的方式来存储获取到的信息，相对繁琐） 



## 2. 代码实现

对于以上提到的这3个子功能的更多参数和使用方式，最好还是去查阅相关的文档，在此如果写下来也是一些技术文档的拷贝，下面给出最后获取物理内存的代码



```asm6502
total_mem_bytes:
    .long 0
//获取到的结构体类似的块信息存储在ards_buf中
ards_buf:
    .fill 244

//保存0x15号中断E820号中断返回结构体的个数
ards_nr:
    .word 0

//代码开始：
   xorl %ebx, %ebx
   movl $0x534d4150, %edx
   movw $ards_buf, %di

//调用中断0x15中e820号中断获取内存
e820_mem_get_loop:
    movl $0x0000e820, %eax
    movl $20, %ecx
    int $0x15
    jc e820_failed_so_try_e801

    addw %cx, %di
    incw ards_nr

    cmpl $0, %ebx
    jnz e820_mem_get_loop

    movw ards_nr, %cx
    movl $ards_buf, %ebx
    xorl %edx,%edx

find_max_mem_area:
    movl (%ebx), %eax
    addl 8(%ebx), %eax
    addl $20, %ebx
    cmpl %eax, %edx
    jge next_ards
    movl %eax, %edx
next_ards:
    loop find_max_mem_area
    jmp mem_get_ok

e820_failed_so_try_e801:
    movw $0xe801, %ax
    int $0x15
    jc e801_failed_so_try88

    movw $0x400, %cx
    mulw %cx
    shll $16, %edx
    andl $0x0000FFFF, %eax
    orl %eax, %edx
    addl $0x100000, %edx
    movl %edx, %esi

    xorl %eax, %eax
    movw %bx, %ax
    movl $0x10000, %ecx
    mull %ecx
    addl %eax, %esi
    movl %esi, %edx
    jmp mem_get_ok

e801_failed_so_try88:
    movb $0x88, %ah
    int $0x15
    jc err_hlt
    andl $0x0000FFFF, %eax

    movw $0x400, %cx
    mulw %cx
    shll $16, %edx
    orl %eax, %edx
    addl $0x100000, %edx

mem_get_ok:
    movl %edx, total_mem_bytes
```
